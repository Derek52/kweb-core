buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
    }
}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version '1.6.10'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.5.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
}

apply plugin: "com.github.ben-manes.versions"

ext {
    ktor_version = '1.6.7'
}

group 'com.github.kwebio'
version '0.10.17'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://plugins.gradle.org/m2' }
}

test {
    useJUnitPlatform()
    systemProperty('sel.jup.default.browser', System.getProperty('sel.jup.default.browser'))
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.google.guava:guava:31.0.1-jre'

    //////////////////////////////
    // Kotlin library dependencies
    //////////////////////////////
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.10"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    ////////////////////
    // Ktor dependencies
    ////////////////////
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-jetty:$ktor_version"
    implementation "io.ktor:ktor-websockets:$ktor_version"
    implementation "io.ktor:ktor-network-tls-certificates:$ktor_version"

    implementation 'io.mola.galimatias:galimatias:0.2.1'

    implementation 'io.github.microutils:kotlin-logging:2.1.21'
    implementation "org.reflections:reflections:0.10.2"

    implementation 'com.github.yamamotoj:cached-property-kotlin:0.1.0'




    ///////////////////////////
    // Dependencies for testing
    ///////////////////////////
    testImplementation 'org.amshove.kluent:kluent:1.68'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
    testImplementation 'ch.qos.logback:logback-classic:1.3.0-alpha13'
    testImplementation "io.github.bonigarcia:selenium-jupiter:3.3.5"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    implementation 'org.jsoup:jsoup:1.12.2'
}

// Not sure why this is necessary, seems like a dependency is turning on build scans.
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

//This is required because kotlin compiler targets 1.6 by default (https://stackoverflow.com/questions/41113268)
//https://kotlinlang.org/docs/reference/using-gradle.html#attributes-specific-for-jvm
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    archiveBaseName = project.name
    archiveClassifier = 'all'
    manifest {
        attributes 'Main-Class': 'kweb.demos.todo.TodoAppKt'
    }
    mergeServiceFiles()
}

dokkaHtml {
    dokkaSourceSets {
        configureEach {
            samples.from(files("src/main/kotlin/samples.kt"))
        }
    }
}
